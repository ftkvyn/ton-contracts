#!/usr/bin/env fift -s

/*
  /------------------------------------------------------------------------\
  | Created for: Telegram (Open Network) Blockchain Contest                |
  |      Task 3: DNS Resolver (Manually controlled)                        |
  >------------------------------------------------------------------------<
  | Script: TRANSFER OWNERSHIP to another public key (dangerous!!)         |
  >------------------------------------------------------------------------<
  | Author: Oleksandr Murzin (tg: @skydev / em: alexhacker64@gmail.com)    |
  |         October 2019                                                   |
  \------------------------------------------------------------------------/
*/

"TonUtil.fif" include
"Asm.fif" include
{ .s 255 halt } : ^ // inspect stack tool

{ cr ."/---------------------------------------[General information]------------------------------------------\" cr
  ."| Usage: " @' $0 type ." <pkfile> <address OR wch-id OR -> <seqno> <new-key-file> [-r or -R]"
  @' $0 $len 26	 swap - { ." " } swap dup 0 <= { 2drop } { times ."|" } cond cr
  ."| Prepares a file to TRANSFER OWNERSHIP of the entire contract to another public key.                  |" cr
  ."| !!! This file must be used ONLY with Manually controlled TON DNS Resolver contract (mdns-create.fif) |" cr
  ."| !!! Resulting query will be saved to <pkfilebs>-<seqno>-oset.boc and needs to be sent to the network |" cr
  .">--------------------------------------[Parameters description]----------------------------------------<" cr
  ."| !!!     You must not specify < and > or [ and ] characters, only text value of the parameter     !!! |" cr
  ."| <pkfile> shall contain filename .tpriv file (with or without extension) corresponding to the current |" cr
  ."|     owner public key of the resolver contract, address of which is provided in the next parameter.   |" cr
  ."| <address> may contain contract address (in any form) or filename of .addr file (with / without ext)  |" cr
  ."|     if address is omitted (provide wch id in that case) it will be calculated out of the .tpriv file |" cr
  ."|     if address is completely omitted (just - is provided) it will be looked for in <pkfile>.addr file|" cr
  ."|   ! it can contain address in well-defined format (workchain_id:hex_address, base64 or base64url)    |" cr
  ."| <seqno> have to match aforementioned smart contract's current seqno (use seqno SC method to get it)  |" cr
  ."| <new-key-file> has to contain FULL NAME (with .tpriv or .tpub extension) of key, to which ownership  |" cr
  ."|     is to be transferred. MAKE SURE IT IS CORRECT, you will not be able to undo this action!!!       |" cr
  ."| -r activates robot mode (output will be more friendly to robots or interfacing program)              |" cr
  ."| -R activates full robot mode (without writing to files at all, make sure to save or process output!) |" cr
  ."\------------------------------------------------------------------------------------------------------/" cr  
  cr 1 halt
} : usage

$# 4 >= ' usage ifnot // Verify argument count
$# 5 <= ' usage ifnot

variable PKB // PKFile base
$1 =: APKFile // can contain extension! .tpriv
$2 "-" $= { 
	APKFile dup $len dup 6 > 
	{ // APKFile has length > 6 (".tpriv"), may contain extension
		6 - $| ".tpriv" $= { dup PKB ! +".addr" } { drop APKFile dup PKB ! } cond
	} {
		2drop APKFile dup PKB ! // APKFile just cant contain extension
	} cond 
} { $2 dup PKB ! } cond =: AAddress
$3 =: ASeqNo
$4 =: ANewKeyFile
def? $5 { @' $5 } { "" } cond =: Flags
// ."PKB " PKB @ type cr

variable RMode     false RMode  !
variable FRMode    false FRMode !
Flags "-r" $= { true RMode ! } if
Flags "-R" $= { true RMode ! true FRMode ! } if

RMode  @ =: RM
FRMode @ =: FRM

APKFile file-exists? {
	APKFile =: PKFile
} {
	APKFile +".tpriv" file-exists? {
		APKFile +".tpriv" =: PKFile
	} {
		."Private key file " APKFile type ." or " APKFile type .".tpriv not found!"
		cr 1 halt
	} cond
} cond

variable BKey
variable UKey

RM { ."Loading private key from " PKFile type ."..." cr } ifnot
PKFile file>B dup Blen dup 64 = {
	."Error: File " PKFile type ." contains corrupted private key material " 
	."of length " . ."(64 expected)" cr 4 halt } ifnot     drop
32 B| over -rot 256 B>u@ swap <b swap B, b> hash = {
	."Error: File " PKFile type ." contains corrupted private key material " 
	."(hash check failed)" cr 5 halt } ifnot
dup BKey ! 256 B>u@ UKey !

variable AddrWC
variable Addr

// If address is number, try to resolve it from the private key
AAddress (number) 1 = {
	"mdns-logic.fif" file-exists? { ."Error: contract logic file mdns-logic.fif "
	."is required to calculate address!" cr "mdns-logic.fc" file-exists? {
	."Because source code exists you should execute" cr 
	."    func mdns-logic.fc -omdnslogic.fif" cr
	."and then try calling this script again!" cr
	} if 66 halt } ifnot
	RM { ."Resolving address from private key..." cr } ifnot
	<b b{0011} s,
		PROGRAM{ "mdns-logic.fif" include }END>c ref,
		<b 0 32 u, BKey @ priv>pub B, null dict, b> ref,
	null dict, b> hash Addr ! AddrWC !
} if

// If address is still not resolved and it is SMCA address in any format
Addr @ null? AAddress $>smca dup { nip -rot 2over and {
	Addr ! AddrWC ! 2drop
} if } { 2drop } cond

// Last resort - consider it as a file name
variable AddrFile
Addr @ null? {
	AAddress file-exists? {
		AAddress AddrFile !
	} { 
		AAddress +".addr" file-exists? {
			AAddress +".addr" AddrFile !
		} { 
			."Error: Unable to determine and parse address in any of possible ways!"
			cr 3 halt
		} cond
	} cond
	RM { ."Loading address from " AddrFile @ type ."..." cr } ifnot
	// Now need to load file from AddrFile
	AddrFile @ file>B dup Blen dup 36 = {
		."Error: Incorrect address file length, expected 36 bytes, got " . ."bytes"
		cr 6 halt
	} ifnot drop
	256 B>u@+ swap 16 B>i@ AddrWC ! Addr !
} if

variable Seq
ASeqNo (number) 1 = { 
."Error: SeqNo must be a 32-bit unsigned integer, provided input is not a number!" cr 10 halt } ifnot
dup 0 < { ."Error: SeqNo must be a 32-bit unsigned integer, provided input is negative!" cr 11 halt } if
dup 32 ufits { ."Error: SeqNo must be a 32-bit unsigned integer, provided input is out of bounds!" cr 12 halt } ifnot 
Seq !
RM not Seq @ 0 = and { ."Warning: Zero SeqNo should not be met in wild, please double-check your input!" cr } if

// The Cat has gone away :-(
// But the cat has changed owner by now ... hope he is okay

ANewKeyFile file-exists?
{ ."Error: New key file " ANewKeyFile type ." does not exist!" 13 halt } ifnot
RM { ."Loading new key from " ANewKeyFile type ."..." cr } ifnot
ANewKeyFile file>B dup Blen dup 64 =
{ // Possibly .tpriv file containing the private key material
	drop ."Attempting load as a private key file..." cr 
	32 B| over -rot 256 B>u@ swap <b swap B, b> hash = {
		."Error: File " ANewKeyFile type ." contains corrupted private key material " 
		."(hash check failed)" cr 14 halt } ifnot 
	priv>pub =: NewPubKey
} { // Okay, lets try another possibility
	96 = {
		."Attempting load as a public key file..." cr // ed25519_chksign: data sig key
		32 B| over dup 3 -roll /* k ksk */ ed25519_chksign {
		."Error: File " ANewKeyFile type ." contains corrupted public key material " 
		."(signature check failed)" cr 16 halt } ifnot 
		=: NewPubKey
	} {
		."Error: File " ANewKeyFile type ." has unknown format!" cr 15 halt
	} cond
} cond

<b  Seq @ 32 u, 51 6 u, 0 1 u, // Farewell, Cat!
	NewPubKey B,
b> dup =: Message

// <s 32 u@+ 6 u@+ 1 u@+ 16 i@+ 6 u@+ 1 pick B@ ^

hashu BKey @ ed25519_sign_uint =: MessageSign

<b 
//  ext_in_msg_info$10
//  ||src:MsgAddressExt addr_none$00
//  ||||dest:MsgAddressInt addr_std$10
//  ||||||anycast:(Maybe Anycast) nothing$0 {X:Type} = Maybe X;
//  |||||||
//  \/\/\/V
  b{1000100} s,
  AddrWC @ Addr @ addr, // workchain_id:int8 address:uint256 = MsgAddressInt;
//   import_fee:Grams = CommonMsgInfo; 0000: zero length = zero
//   || init:(Maybe (Either StateInit ^StateInit))
//   || nothing$0 {X:Type} value:X = Maybe X;
//   || |body:(Either X ^X) = Message X;
//   || ||
//  \\//V\----\ Will select a bit bit later!
  b{00000} s,
  dup b> <s sbits 1+ 1022 swap - // remaining free space in slice (-1 to be safe)
  <b MessageSign B, Message <s s, dup b> <s sbits // used bits by Sign+Message
  rot <= 
  /* Inline */ { swap b{0} s, swap b> <s s, } 
  /* Ref-ed */ { swap b{1} s, swap b> ref, } cond
b> constant ExtMessage

PKB @ +"-" Seq @ (.) $+ +"-oset.boc" =: BocFile
RM { BocFile file-exists? { ."Warning! BoC file will be overwritten!" cr } if } ifnot

ExtMessage 2 boc+>B dup Blen =: EMLen =: EM

FRM { EM BocFile B>file
 RM { ."Query saved to " BocFile type cr } ifnot
} ifnot

RMode @ { cr 
	."/-[Success: The query to CHANGE OWNERSHIP of the entire contract has been created!]" cr
	."| New key: " NewPubKey Bx. cr
	."| Think twice before commiting this to the network, you will not be able to undo later!" cr
	.">-> The query have been saved to <path-to-file>/" BocFile type cr
	."| You need to use some tool to send the query to the network!" cr
	."| For example, you can use lite-client's sendfile method, just like" cr
	."\-[ sendfile <path-to-file>/" BocFile type ." ]" cr cr
} { 
	."type=oset" cr
	."pk_file=" PKFile type cr
	."sc_addr=" AddrWC @ Addr @ .addr cr
FRM { ."boc_file=" BocFile type cr } ifnot
	."seqno=" Seq @ (.) type cr
	."new_key=" NewPubKey Bx. cr
	."query=" EM Bx. cr
	."query_len=" EMLen (.) type cr
} swap cond