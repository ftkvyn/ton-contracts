#!/usr/bin/fift -s
"TonUtil.fif" include
"Asm.fif" include

"_mdns.fif" include <s =: contract_code
{ dup rot dup rot = { drop drop } { "Error: Got " swap (.) $+ " should be " $+ swap (.) $+ abort } cond } : should_be


"1.pk" load-generate-keypair
constant wallet1_privk
256 B>u@ constant wallet1_pubk

"2.pk" load-generate-keypair
constant wallet2_privk
256 B>u@ constant wallet2_pubk

"3.pk" load-generate-keypair
constant wallet3_privk
256 B>u@ constant wallet3_pubk

"4.pk" load-generate-keypair
constant wallet4_privk
256 B>u@ constant wallet4_pubk


0xC7EAFBC106A7AA4BA3D16007C6AC64CAAC1078B4A43577339E246F466405E896 =: contract_address_int
<b b{100} s, 0 8 u, contract_address_int 256 u, b> <s =: contract_address_full

0 tuple 0x076ef1ea , // magic
0 , 0 , // actions msg_sents
1570470873 , // unix_time
1 , 1 , 0 , // block_lt, trans_lt, rand_seed
 0 tuple 9223372036854775807 , dictnew , , // reamining balance
contract_address_full , dictnew , // contract_address, global_config
1 tuple // wrap to another tuple
constant c7


<b 0 8 u, b> <s constant init_message
-1 =: recv_external
0 =: recv_internal
<b 0 32 u, wallet1_pubk 256 u, dictnew dict, dictnew dict, b> =: contract_storage 

init_message recv_external contract_code contract_storage c7 runvmctx
=: initialised_storage
0 should_be 

123660 =: dnsresolve
120129 =: get_expiration_info

"baddaboo" =: domain_name
<b 1 1 u, b> <s
1
dictnew
16
idict!
drop
=: dnsrecord
<b 2 2 u, b> <s
2
dnsrecord
16
idict!
drop
=: dnsrecord

<b 1 1 u, b> <s
-1
dnsrecord
16
idict!
drop
=: dnsrecord_redirect

<b 1 32 u, 1 8 u, <b domain_name $, b> ref, dnsrecord dict, b> =: message_body
message_body hashu wallet1_privk ed25519_sign_uint =: signature
<b signature B, message_body <s s, b> =: full_message 
full_message <s csr. cr

<b 1 32 u, 1 8 u, <b domain_name $, b> ref, dnsrecord_redirect dict, b> =: message_body_rdr
message_body_rdr hashu wallet1_privk ed25519_sign_uint =: signature
<b signature B, message_body_rdr <s s, b> =: full_message_body_rdr

<b full_message ref, b> <s recv_external contract_code initialised_storage c7 runvmctx
=: storage_with_baddaboo_s1
0 should_be

<b full_message_body_rdr ref, b> <s recv_external contract_code initialised_storage c7 runvmctx
=: storage_with_baddaboo_s1_redirect
0 should_be


<b domain_name $, b> <s 2 dnsresolve contract_code storage_with_baddaboo_s1 c7 runvmctx
drop // storage
0 should_be // error_code
drop domain_name $len 1+ 8 * should_be


<b domain_name 0 hold +"4" $, b> <s 0 dnsresolve contract_code storage_with_baddaboo_s1 c7 runvmctx
drop // storage
0 should_be // error_code
drop 0 should_be

<b domain_name 0 hold +"4" $, b> <s 0 dnsresolve contract_code storage_with_baddaboo_s1_redirect c7 runvmctx
.s
drop // storage
0 should_be // error_code
drop domain_name $len 1+ 8 * should_be

<b domain_name $, b> <s 77 dnsresolve contract_code storage_with_baddaboo_s1_redirect c7 runvmctx
.s
drop // storage
0 should_be // error_code
drop 0 should_be
// Domain exists, but no such category


// хотим убрать запись 2
// хотим добавить запись 3
<b b> <s
2
dictnew
16
idict!
drop
=: dnsrecord_remove

<b 3 3 u, b> <s
3
dictnew
16
idict!
drop
=: dnsrecord_add



<b dnsrecord_add dict, dnsrecord_remove dict, b> =: records_update
<b dictnew dict, dictnew dict, b> =: domains_update

<b 2 32 u, 2 8 u, <b domain_name $, b> ref, records_update ref, domains_update ref, b> =: message_body_update
message_body_update hashu wallet1_privk ed25519_sign_uint =: signature
<b signature B, message_body_update <s s, b> =: full_message_body_update



<b full_message_body_update ref, b> <s recv_external contract_code storage_with_baddaboo_s1 c7 runvmctx
=: storage_with_baddaboo_updates
0 should_be

<b domain_name $, b> <s 2 dnsresolve contract_code storage_with_baddaboo_updates c7 runvmctx
.s
drop // storage
0 should_be // error_code
drop 0 should_be

<b domain_name $, b> <s 3 dnsresolve contract_code storage_with_baddaboo_updates c7 runvmctx
.s
drop // storage
0 should_be // error_code
drop domain_name $len 1+ 8 * should_be


."Ok" cr
