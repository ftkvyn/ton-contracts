constant code

"TonUtil.fif" include

{ ."usage: " @' $0 type ." <workchain-id> [<filename-base>]" cr
  ."Creates a new manual dns resolver in specified workchain, with private key saved to or loaded from <filename-base>.pk" cr
  ."('new-manual-dns.pk' by default)" cr 1 halt
} : usage
$# 1- -2 and ' usage if

$1 parse-workchain-id =: wc    // set workchain id from command line argument
def? $2 { @' $2 } { "new-manual-dns" } cond constant file-base

."Creating new manual dns resolver in workchain " wc . cr

<b 0 32 u, 
   file-base +".pk" load-generate-keypair
   constant dns_pk
   B, 
b> constant data
<b b{0011} s, code ref, data ref, null dict, b> constant state-init  // create StateInit
state-init .s
state-init ."StateInit: " <s csr. cr
state-init hash wc swap 2dup 2constant dns_addr
."new manual dns resolver address = " 2dup .addr cr
2dup file-base +".addr" save-address-verbose
."Non-bounceable address (for init): " 2dup 7 .Addr cr
."Bounceable address (for later access): " 6 .Addr cr
<b 7 8 u, b> constant msg // mode = 7 - initializing the contract.
msg
.s cr
dup ."signing message: " <s csr. cr
dup hash dns_pk ed25519_sign_uint rot
drop
<b b{1000100} s, dns_addr addr, b{000010} s, state-init <s s, b{0} s, swap B, swap <s s, b>
dup ."External message for initialization is " <s csr. cr
2 boc+>B dup Bx. cr
file-base +"-query.boc" tuck B>file
."(Saved manual dns creating query to file " type .")" cr
